from building import *
import rtconfig

cwd = GetCurrentDir()
src = ['spi_core.c', 'spi_dev.c']
CPPPATH = [cwd, cwd + '/../include']
LOCAL_CCFLAGS = ''

if GetDepend('ENABLE_QSPI'):
    src += ['qspi_core.c']

src_device = []

if GetDepend('ENABLE_SPI_WIFI'):
    src_device += ['spi_wifi_rw009.c']

if GetDepend('ENABLE_ENC28J60'):
    src_device += ['enc28j60.c']

if GetDepend('ENABLE_SPI_MSD'):
    src_device += ['spi_msd.c']

if GetDepend('ENABLE_SFUD'):
    src_device += ['spi_flash_sfud.c', 'sfud/src/sfud.c']
    CPPPATH += [cwd + '/sfud/inc']
    if GetDepend('SFUD_ENABLE_SFDP'):
        src_device += ['sfud/src/sfud_sfdp.c']
    if rtconfig.CROSS_TOOL == 'gcc':
        LOCAL_CCFLAGS += ' -std=c99'
    elif rtconfig.CROSS_TOOL == 'keil':
        LOCAL_CCFLAGS += ' --c99'

src += src_device

group = DefineGroup('DeviceDrivers', src, depend = ['ENABLE_SPI'], CPPPATH = CPPPATH, LOCAL_CCFLAGS = LOCAL_CCFLAGS)

Return('group')
