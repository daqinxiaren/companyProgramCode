Import('RTT_ROOT')
Import('rtconfig')
from building import *

cwd     = GetCurrentDir()
CPPPATH = [cwd, str(Dir('#'))]

src  = ['hwcrypto.c']

if (GetDepend(['HARDWARE_CRYPTO_ENABLE_AES'])  or
    GetDepend(['HARDWARE_CRYPTO_ENABLE_DES'])  or
    GetDepend(['HARDWARE_CRYPTO_ENABLE_3DES']) or
    GetDepend(['HARDWARE_CRYPTO_ENABLE_RC4'])):
    src += ['hw_symmetric.c']
    if GetDepend(['HARDWARE_CRYPTO_ENABLE_GCM']):
        src += ['hw_gcm.c']

if (GetDepend(['HARDWARE_CRYPTO_ENABLE_MD5'])  or
    GetDepend(['HARDWARE_CRYPTO_ENABLE_SHA1']) or
    GetDepend(['HARDWARE_CRYPTO_ENABLE_SHA2'])):
    src += ['hw_hash.c']

if GetDepend(['HARDWARE_CRYPTO_ENABLE_RNG']):
    src += ['hw_rng.c']

if GetDepend(['HARDWARE_CRYPTO_ENABLE_CRC']):
    src += ['hw_crc.c']

if GetDepend(['HARDWARE_CRYPTO_ENABLE_BIGNUM']):
    src += ['hw_bignum.c']

group = DefineGroup('DeviceDrivers', src, depend = ['ENABLE_HWCRYPTO'], CPPPATH = CPPPATH)

Return('group')
